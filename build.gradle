/**
 * @author Hung Nguyen (hungng@seas.upenn.edu)
 */

plugins {
    id 'com.github.johnrengelman.shadow' version '2.0.4'
}
apply plugin: 'maven-publish'

group 'edu.upenn.cis.precise.openicelite'

wrapper {
    gradleVersion = '4.8'
}

ext {
    VERSION_NUMBER = '0.1.1'
    SOURCE_COMPATIBILITY = 1.8
    TARGET_COMPATIBILITY = 1.8
}

allprojects {
    apply plugin: 'java'
    defaultTasks 'build'

    sourceCompatibility = SOURCE_COMPATIBILITY
    targetCompatibility = TARGET_COMPATIBILITY
    version = VERSION_NUMBER

    repositories {
        flatDir {
            dirs 'libs'
        }

        jcenter()
    }
}

subprojects {
    dependencies {
        // Logging
        implementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.11.0'
        implementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.11.0'

        // JSON
        implementation group: 'org.json', name: 'json', version: '20180130'
    }
}

dependencies {
    implementation subprojects.each { project(it.path) }
}

shadowJar {
    baseName = 'openice-lite'
    classifier = null
}

publishing {
    publications {
        shadow(MavenPublication) { publication ->
            project.shadow.component(publication)
        }
    }
}

task javadoc(overwrite: true, type: Javadoc, group: 'documentation') {
    destinationDir = file("docs")
    title = "OpenICE-lite $VERSION_NUMBER API"
    options.author true
    include "edu/upenn/cis/precise/openicelite/**/*"

    subprojects.each { proj ->
        proj.tasks.withType(Javadoc).each { task ->
            source += task.source
            classpath += task.classpath
            excludes += task.excludes
        }
    }
}
